buildscript {
	ext {
		baseName = 'ygo-api'
		springVersion = '2.4.2'
		swagger2Version = '3.0.0'
		javadocVersion = '3.1.1'
		cache2kVersion = '1.2.4.Final'
		lombokVersion = '1.18.12'
		mysqlVersion = '8.0.21'
		jacksonVersion = "2.11.2"
		cucumberVersion = "6.7.0"
		gatlingVersion = "3.5.0"
		restAssuredVersion = '4.3.3'
		groovyVersion = '3.0.7'
	}
}

plugins {
	id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply true
	id 'org.springframework.boot' version '2.3.3.RELEASE' apply true
	id 'jacoco' apply true
	id 'java' apply true
}


group = 'com.rtomyj.ygo'
archivesBaseName = 'ygo-api'
version = '1.2.3'
sourceCompatibility = '11'
targetCompatibility = '11'


apply from: 'gradle/testConfig.gradle'
apply from: 'gradle/integrationTest.gradle'
apply from: 'gradle/performanceTest.gradle'


repositories {
	mavenCentral()
	maven {
		url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/'	// used for snapshots
	}
}


sourceSets {
	build {
		java.srcDir file('src/main/java')
	}
}


dependencies {
	implementation("javax.validation:validation-api:2.0.1.Final")

	implementation("org.springframework.boot:spring-boot-starter-web:$springVersion")
	implementation("org.springframework.boot:spring-boot-starter-hateoas:$springVersion")
	implementation("org.springframework.boot:spring-boot-starter-data-jpa:$springVersion")
	implementation("org.springframework.boot:spring-boot-starter-log4j2:$springVersion")
	implementation("org.springframework.boot:spring-boot-starter-actuator:$springVersion")

	implementation("org.springframework.boot:spring-boot-starter-jetty:$springVersion")
	implementation("org.eclipse.jetty:jetty-alpn-conscrypt-server")
	implementation("org.eclipse.jetty.http2:http2-server")

	implementation("org.apache.maven.plugins:maven-javadoc-plugin:$javadocVersion")

	implementation("io.springfox:springfox-boot-starter:$swagger2Version")

	runtimeOnly("mysql:mysql-connector-java:$mysqlVersion")

	implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")

	implementation("org.cache2k:cache2k-api:$cache2kVersion")
	implementation("org.cache2k:cache2k-core:$cache2kVersion")

	implementation("com.google.guava:guava:30.1-jre")

	annotationProcessor("org.projectlombok:lombok:$lombokVersion")	// needed to compile via gradle CLI
	implementation("org.projectlombok:lombok:$lombokVersion")	// plug in required to work in VSCode, might be the same for other IDE's

}


configurations {
	implementation {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
		exclude module: 'spring-boot-starter-tomcat'
		exclude group: 'org.apache.tomcat'
		exclude group: 'junit'
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	compileOnly {
		extendsFrom annotationProcessor
	}
}


bootJar {
	group 'Build'
	description 'Creates a JAR file that can be executed to launch YGO service'

	manifest {
			attributes 'Implementation-Title': archivesBaseName
	}
}


task bootJarPath {
	doFirst {
		println "${buildDir}/libs/${project.archivesBaseName}-${project.version}.jar"
	}
}


javadoc {

	options.memberLevel = JavadocMemberLevel.PRIVATE
	source = sourceSets.main.allJava

}