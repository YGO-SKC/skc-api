dependencies {

    /* Test level dependencies */
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springVersion"
    testRuntimeOnly "com.h2database:h2"

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"

    testImplementation 'io.rest-assured:rest-assured:4.2.0'
    testImplementation 'io.rest-assured:json-path:4.2.0'
    testImplementation 'io.rest-assured:xml-path:4.2.0'
    testImplementation 'io.rest-assured:json-schema-validator:4.2.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"

    testImplementation("io.gatling:gatling-core:${gatlingVersion}")
    testImplementation group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: '3.5.0'

}


configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }

    testImplementation {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}


test {
    useJUnitPlatform()

    minHeapSize = "256m"
    maxHeapSize = "512m"
}


task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'cucumber', 'src/test/resources/cucumber']
        }
    }
}


tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors() / 3 ?: 1
}


task perfTest(type: JavaExec) {
    description = 'Performance test executed using Gatling'
    group = 'Test'
    classpath = sourceSets.test.runtimeClasspath
    jvmArgs = [
            // workaround for https://github.com/gatling/gatling/issues/2689
            "-Dgatling.core.directory.binaries=${sourceSets.test.output.classesDirs.toString()}"
    ]
    main = 'io.gatling.app.Gatling'
    args = [
            '--simulation', 'simulations.BrowseSimulation',
            '--results-folder', "${buildDir}/gatling-results",
            '--binaries-folder', sourceSets.test.output.classesDirs.toString() // ignored because of above bug
    ]
}
